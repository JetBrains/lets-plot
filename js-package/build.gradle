plugins {
    id "org.jetbrains.kotlin.multiplatform"
}

kotlin {
    js() {
        browser()
        binaries.executable()
    }
    sourceSets {
        jsMain {
            dependencies {
                implementation("org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version")

                implementation project(':commons')
                implementation project(':datamodel')
                implementation project(':canvas')
                implementation project(':plot-base')
                implementation project(':plot-builder')
                implementation project(':plot-stem')
                implementation project(':platf-w3c')
                implementation project(':gis')
                implementation project(":livemap")
                implementation project(':plot-livemap')

                implementation("io.ktor:ktor-client-websockets-js:$ktor_version")
                implementation("io.ktor:ktor-client-js:$ktor_version")
                implementation("io.github.microutils:kotlin-logging-js:$kotlinLogging_version")
            }
        }
    }
}

def dist_dir = "${project.buildDir}/distributions/"
def artifact_name = "js-package.js"

tasks.register('renameProdJsArtifact') {
    group = project.letsPlotTaskGroup
    doLast{
        copy {
            from "${dist_dir}${artifact_name}"
            rename "${artifact_name}", "lets-plot.min.js"
            into dist_dir
        }
    }
}

tasks.register('renameDevJsArtifact') {
    group = project.letsPlotTaskGroup
    doLast{
        copy {
            from "${project.buildDir}/developmentExecutable/${artifact_name}"
            rename "${artifact_name}", "lets-plot.js"
            into dist_dir
        }
    }
}

jsBrowserDevelopmentWebpack.finalizedBy renameDevJsArtifact
jsBrowserProductionWebpack.finalizedBy renameProdJsArtifact

tasks.register('copyForPublish', Copy) {
    dependsOn jsBrowserProductionWebpack

    from "${dist_dir}${artifact_name}"
    rename "${artifact_name}", "lets-plot.min.js"
    into "${rootDir}/js-package/distr/"
}
