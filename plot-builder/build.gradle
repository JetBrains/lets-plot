plugins {
    id "org.jetbrains.kotlin.multiplatform"
    id "org.jetbrains.gradle.plugin.idea-ext"
}

kotlin {
    jvm()
    js {
        browser()
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
//                implementation "io.github.microutils:kotlin-logging:$kotlinLogging_version"
                implementation project(':base-portable')
                implementation project(':base')
                implementation project(':vis-svg-portable')
                implementation project(':vis-canvas')
                implementation project(':plot-base-portable')
                implementation project(':plot-common-portable')
                implementation project(':plot-common')
                implementation project(':plot-builder-portable')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
                implementation project(':test-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "io.github.microutils:kotlin-logging-jvm:$kotlinLogging_version"
                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
                implementation "org.hamcrest:hamcrest-core:$hamcrest_version"
                implementation "org.hamcrest:hamcrest-library:$hamcrest_version"
                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
                implementation "org.mockito:mockito-core:$mockito_version"
                implementation "org.assertj:assertj-core:$assertj_version"
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "io.github.microutils:kotlin-logging-js:$kotlinLogging_version"
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

idea {
    module {
        settings {
            packagePrefix["src/jvmMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmTest/kotlin"] = "jetbrains.datalore"
        }
    }
}


/*
 * Copyright (c) 2019. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

// Workaround for Idea/Gradle bug: https://youtrack.jetbrains.com/issue/KT-24463
// MPP: Run does not add resource directory to classpath [Cannot get resource when using common module]
task copyProcessedResources(type: Copy) {
    from "${project.buildDir}/processedResources"
    into "${project.buildDir}/classes/kotlin"
}

build.dependsOn += copyProcessedResources

// Fix Gradle 7 error:
// Execution failed for task ':plot-builder:jvmJar'.
// Entry svgMapper/jfx/plot.css is a duplicate but no duplicate handling strategy has been set.
jvmJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
