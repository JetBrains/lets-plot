plugins {
    id "org.jetbrains.kotlin.multiplatform"
    id "org.jetbrains.gradle.plugin.idea-ext"
}


/*
 * Copyright (c) 2019. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

kotlin {
    jvm()
    jvm("jvmBatik")
    jvm("jvmJfx")
    jvm("jvmBrowser")
    js(LEGACY) {
        browser {
            dceTask {
                keep.addAll(
                        "lets-plot-plot-demo.jetbrains.datalore.plotDemo.component.axisComponentDemo",
                        "lets-plot-plot-demo.jetbrains.datalore.plotDemo.plotAssembler.linearRegressionDemo",
                        "lets-plot-plot-demo.jetbrains.datalore.plotDemo.plotAssembler.loessRegressionDemo",
                )
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')

                implementation project(':base-portable')
                implementation project(':base')
                implementation project(':mapper-core')
                implementation project(':datamodel')
                implementation project(':vis-svg-mapper')
                implementation project(':plot-base-portable')
                implementation project(':plot-common-portable')
                implementation project(':plot-builder-portable')
                implementation project(':plot-builder')
                implementation project(':plot-config-portable')
                implementation project(':plot-config')
//                implementation project(':plot-image-export')
                implementation project(':plot-demo-common')
                implementation project(':vis-demo-common')
            }
        }

        allJvm {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                compileOnly "io.github.microutils:kotlin-logging-jvm:$kotlinLogging_version"
                implementation "org.slf4j:slf4j-simple:$slf4j_version"  // Enable logging to console
            }
        }
        jvmMain {
            dependsOn allJvm
        }
        jvmBatikMain {
            dependsOn allJvm
            dependsOn jvmMain
            dependencies {
                implementation project(':vis-svg-mapper-batik')
                implementation project(':vis-demo-common-batik')
                implementation project(':vis-swing-common')
                implementation project(':vis-swing-batik')
            }
        }
        jvmJfxMain {
            dependsOn allJvm
            dependsOn jvmMain
            dependencies {
                implementation project(':vis-canvas')
                implementation project(':vis-svg-mapper-jfx')
                implementation project(':vis-demo-common-jfx')
                implementation project(':vis-swing-common')
                implementation project(':vis-swing-jfx')

                compileOnly("org.openjfx:javafx-base:$jfx_version:${jfx_platform()}")
                compileOnly("org.openjfx:javafx-graphics:$jfx_version:${jfx_platform()}")
                compileOnly("org.openjfx:javafx-swing:$jfx_version:${jfx_platform()}")
            }
        }
        jvmBrowserMain {
            dependsOn allJvm
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-html-jvm:${kotlinx_html_version}"
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }
    }
}

idea {
    module {
        settings {
            packagePrefix["src/jvmMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmBatikMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmJfxMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmBrowserMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmTest/kotlin"] = "jetbrains.datalore"
        }
    }
}

// Fix Gradle 7 error:
// Execution failed for task ':plot-demo:jvm*Jar'.
// Entry diamonds.csv is a duplicate but no duplicate handling strategy has been set.
jvmJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jvmBatikJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jvmBrowserJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jvmJfxJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
