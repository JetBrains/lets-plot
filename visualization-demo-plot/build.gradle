plugins {
    id 'kotlin-multiplatform'
    id 'org.jetbrains.gradle.plugin.idea-ext'
    id "com.moowork.node"
}
repositories {
    mavenCentral()
}

node {
    version = '10.16.3'
    npmVersion = '6.9.0'
    download = true
}

repositories.whenObjectAdded {
    if (it instanceof IvyArtifactRepository) {
        metadataSources {
            artifact()
        }
    }
}

kotlin {
    jvm()
    jvm("jvmBatik")
    jvm("jvmJfx")
    jvm("jvmBrowser")
    js() {
        compileKotlinJs {
            kotlinOptions.sourceMap = true
            kotlinOptions.sourceMapEmbedSources = "always"
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

                implementation project(':base-portable')
                implementation project(':base')
                implementation project(':mapper-core')
                implementation project(':visualization-base-svg')
                implementation project(':visualization-base-svg-mapper')
                implementation project(':visualization-plot-base-portable')
                implementation project(':visualization-plot-base')
                implementation project(':visualization-plot-common-portable')
                implementation project(':visualization-plot-common')
                implementation project(':visualization-plot-builder-portable')
                implementation project(':visualization-plot-builder')
                implementation project(':visualization-plot-config-portable')
                implementation project(':visualization-plot-config')
                implementation project(':visualization-demo-common')
            }
        }

        allJvm {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                compileOnly "io.github.microutils:kotlin-logging:$kotlinLogging_version"
            }
        }
        jvmMain {
            dependsOn allJvm
        }
        jvmBatikMain {
            dependsOn allJvm
            dependsOn jvmMain
            dependencies {
                implementation "org.apache.commons:commons-math3:$math3_version"
                implementation project(':visualization-base-svg-mapper-batik')
            }
        }
        jvmJfxMain {
            dependsOn allJvm
            dependsOn jvmMain
            dependencies {
                implementation "org.apache.commons:commons-math3:$math3_version"
                implementation project(':visualization-base-canvas')
                implementation project(':visualization-base-svg-mapper-jfx')
                implementation project(':visualization-demo-common-jfx')
            }
        }
        jvmBrowserMain {
            dependsOn allJvm
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-html-jvm:${kotlinx_html_version}"
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }
    }
}

idea {
    module {
        settings {
            packagePrefix["src/jvmMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmBatikMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmJfxMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmBrowserMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmTest/kotlin"] = "jetbrains.datalore"
        }
    }
}


// Workaround for Idea/Gradle bug: https://youtrack.jetbrains.com/issue/KT-24463
// MPP: Run does not add resource directory to classpath [Cannot get resource when using common module]
// 
// JavaFX Scene mapping requires stylesheet resource URI 
task copyProcessedResources(type: Copy) {
    from "${project.buildDir}/processedResources"
    into "${project.buildDir}/classes/kotlin"
}

build.dependsOn += copyProcessedResources


// ---------------------------------
// Trick to copy kotlin.js etc. to `output`
configurations {
    libraryDependenciesJs
    projectDependenciesJs
}

dependencies {
    libraryDependenciesJs "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    libraryDependenciesJs "io.github.microutils:kotlin-logging-js:$kotlinLogging_version"

    projectDependenciesJs project(':base-portable')
    projectDependenciesJs project(':base')
    projectDependenciesJs project(':mapper-core')
    projectDependenciesJs project(':visualization-base-svg')
    projectDependenciesJs project(':visualization-base-svg-mapper')

    projectDependenciesJs project(':visualization-demo-common')

    projectDependenciesJs project(':visualization-base-canvas')         // required by plot-builder (get rid?)

    projectDependenciesJs project(':visualization-plot-common-portable')
    projectDependenciesJs project(':visualization-plot-common')
    projectDependenciesJs project(':visualization-plot-base-portable')
    projectDependenciesJs project(':visualization-plot-base')
    projectDependenciesJs project(':visualization-plot-builder-portable')
    projectDependenciesJs project(':visualization-plot-builder')
    projectDependenciesJs project(':visualization-plot-config-portable')
    projectDependenciesJs project(':visualization-plot-config')
}

// ---------------------------------
def OUTPUT_DIR_JS = "${project.buildDir}/demoWeb"
def OUTPUT_DIR_JS_LIB = "${OUTPUT_DIR_JS}/lib"

task copyLibraryDependenciesJs(type: Copy) {
    configurations.libraryDependenciesJs.each {
        println "library dependency: ${it.absolutePath}"
        from zipTree(it.absolutePath).matching { include '*.js' }
    }
    into OUTPUT_DIR_JS_LIB
}

task copyProjectDependenciesJs(type: Copy) {
    configurations.projectDependenciesJs.each {
        def jarJs = it.absolutePath.replace("-metadata-", "-js-")
        println "project dependency: $jarJs"
        from zipTree(jarJs).matching { include '*.js' }
    }
    into OUTPUT_DIR_JS_LIB
}

task copyMainJs(type: Copy) {
    from "${project.buildDir}/classes/kotlin/js/main"
    include "*.js"
    into OUTPUT_DIR_JS
}

task webpackBuild(type: NpmTask) {
    args = ['run', 'webpack-build']
}

webpackBuild.dependsOn += copyLibraryDependenciesJs
webpackBuild.dependsOn += copyProjectDependenciesJs
webpackBuild.dependsOn += copyMainJs
webpackBuild.dependsOn += npmInstall
assemble.dependsOn += webpackBuild

//println "======================================="
//build.dependsOn.forEach {
//    println it
//}
