plugins {
    id "org.jetbrains.kotlin.multiplatform"
    id "maven-publish"
    id "signing"
}

project.ext {
    batikGroupId = "org.apache.xmlgraphics"
    batikArtifacts = [
            "batik-transcoder",
            "batik-codec"
    ]

    tiffioGroupId = "com.twelvemonkeys.imageio"
    tiffioArtifact = "imageio-tiff"
    tiffioVersion = twelvemonkeys_imageio_version
}

kotlin {
    jvm()

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')

                implementation project(':commons')
                implementation project(':datamodel')
                implementation project(':plot-stem')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "io.github.microutils:kotlin-logging-jvm:$kotlinLogging_version"

                implementation project(':platf-awt')

                batikArtifacts.forEach {
                    api "$batikGroupId:$it:$batik_version"
                }

                // TIFF support
                implementation "$tiffioGroupId:$tiffioArtifact:$tiffioVersion"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
                implementation "org.hamcrest:hamcrest-core:$hamcrest_version"
                implementation "org.hamcrest:hamcrest-library:$hamcrest_version"
                implementation "org.mockito:mockito-core:$mockito_version"
                implementation "org.assertj:assertj-core:$assertj_version"

                implementation project(':demo-and-test-shared')
            }
        }
    }
}

// Publishing

def artifactBaseName = 'lets-plot-image-export'
def artifactGroupId = project.group
def artifactVersion = project.version

jvmJar {
    archiveFileName = "$artifactBaseName-${artifactVersion}.jar"

    // Add LICENSE file to the META-INF folder inside published JAR files.
    metaInf {
        from("$rootDir") {
            include "LICENSE"
        }
    }
}

jvmSourcesJar {
    archiveFileName = "$artifactBaseName-$artifactVersion-sources.jar"
}

publishing {
    publications {
        letsPlotImageExport(MavenPublication) {
            groupId artifactGroupId
            artifactId artifactBaseName
            version artifactVersion

            artifact jvmJar
            artifact jvmSourcesJar
            artifact rootProject.jarJavaDocs

            pom {
                name = "Lets-Plot raster image export"
                description = "Exporting a plot to a raster image (PNG,JPG or TIFF)."
                url = "https://github.com/JetBrains/lets-plot"
                licenses {
                    license {
                        name = "MIT"
                        url = "https://raw.githubusercontent.com/JetBrains/lets-plot/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "jetbrains"
                        name = "JetBrains"
                        email = "lets-plot@jetbrains.com"
                    }
                }
                scm {
                    url = "https://github.com/JetBrains/lets-plot"
                }
                // Dependencies
                withXml {
                    def deps = asNode().appendNode('dependencies')
                    // Apache Batik dependencies
                    batikArtifacts.forEach {
                        def dep = deps.appendNode('dependency')
                        dep.appendNode('groupId', batikGroupId)
                        dep.appendNode('artifactId', it)
                        dep.appendNode('version', batik_version)
                    }

                    def dep = deps.appendNode('dependency')
                    dep.appendNode('groupId', tiffioGroupId)
                    dep.appendNode('artifactId', tiffioArtifact)
                    dep.appendNode('version', tiffioVersion)

                    // Lets-Plot core 'platf-awt' dependency
                    dep = deps.appendNode('dependency')
                    dep.appendNode('groupId', project.group)
                    dep.appendNode('artifactId', "platf-awt")
                    dep.appendNode('version', project.version)
                }
            }
        }
    }
    repositories {
        mavenLocal {
            url = uri(project.localMavenRepository)
        }
    }
}

signing {
    if (!project.version.contains('SNAPSHOT')) {
        sign publishing.publications.letsPlotImageExport
    }
}
