/*
 * Copyright (c) 2019. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

plugins {
    id "org.jetbrains.kotlin.multiplatform"
    id "maven-publish"
    id "signing"
}

kotlin.jvm()

def artifactBaseName = 'lets-plot-common'
def artifactGroupId = project.group
def artifactVersion = project.version

jvmJar {
    archiveFileName = "$artifactBaseName-${artifactVersion}.jar"

    // Add LICENSE file to the META-INF folder inside published JAR files.
    metaInf {
        from("$rootDir") {
            include "LICENSE"
        }
    }
}

def pom_dependencies = [
        // Lets-Plot core artifacts.
        [project.group, "commons-jvm", project.version],
        [project.group, "datamodel-jvm", project.version],
        [project.group, "plot-base-jvm", project.version],
        [project.group, "plot-builder-jvm", project.version],
        [project.group, "plot-stem-jvm", project.version],

        [project.group, "deprecated-in-v4-jvm", project.version],

        // Kotlin logging.
        ["io.github.microutils", "kotlin-logging", kotlinLogging_version],
]

publishing {
    publications {
        letsPlotJvmCommon(MavenPublication) {
            groupId artifactGroupId
            artifactId artifactBaseName
            version artifactVersion

            artifact jvmJar

            pom {
                name = "Lets-Plot common modules"
                description = "Lets-Plot JVM package without the actual rendering."
                url = "https://github.com/JetBrains/lets-plot"
                licenses {
                    license {
                        name = "MIT"
                        url = "https://raw.githubusercontent.com/JetBrains/lets-plot/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "jetbrains"
                        name = "JetBrains"
                        email = "lets-plot@jetbrains.com"
                    }
                }
                scm {
                    url = "https://github.com/JetBrains/lets-plot"
                }
                // Dependencies
                withXml {
                    def deps = asNode().appendNode('dependencies')
                    pom_dependencies.each {
                        def dep = deps.appendNode('dependency')
                        dep.appendNode('groupId', it[0])
                        dep.appendNode('artifactId', it[1])
                        dep.appendNode('version', it[2])
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal {
            url = uri(project.localMavenRepository)
        }
    }
}

signing {
    if (!project.version.contains('SNAPSHOT')) {
        sign publishing.publications.letsPlotJvmCommon
    }
}
