/*
 * Copyright (c) 2019. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

plugins {
    id "org.jetbrains.kotlin.multiplatform"
    id "maven-publish"
    id "signing"
}

kotlin.jvm()

def artifactBaseName = 'lets-plot-jfx'
def artifactGroupId = project.group
def artifactVersion = project.version

def pom_dependencies = [
        // Lets-Plot JVM common
        [project.group, "lets-plot-common", project.version],

        // Lets-Plot core artifacts.
        [project.group, "platf-awt-jvm", project.version],
        [project.group, "platf-jfx-swing-jvm", project.version],
]

publishing {
    publications {
        letsPlotJvmJfx(MavenPublication) {
            groupId artifactGroupId
            artifactId artifactBaseName
            version artifactVersion

            artifact jvmJar

            pom {
                name = "Lets-Plot for JavaFX"
                description = "Lets-Plot JVM package with Swing/JavaFX rendering"
                url = "https://github.com/JetBrains/lets-plot"
                licenses {
                    license {
                        name = "MIT"
                        url = "https://raw.githubusercontent.com/JetBrains/lets-plot/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "jetbrains"
                        name = "JetBrains"
                        email = "lets-plot@jetbrains.com"
                    }
                }
                scm {
                    url = "https://github.com/JetBrains/lets-plot"
                }
                // Dependencies
                withXml {
                    def deps = asNode().appendNode('dependencies')
                    pom_dependencies.each {
                        def dep = deps.appendNode('dependency')
                        dep.appendNode('groupId', it[0])
                        dep.appendNode('artifactId', it[1])
                        dep.appendNode('version', it[2])
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal {
            url = uri(project.localMavenRepository)
        }
    }
}

signing {
    if (!project.version.contains('SNAPSHOT')) {
        sign publishing.publications.letsPlotJvmJfx
    }
}

