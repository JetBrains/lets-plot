plugins {
    id "org.jetbrains.kotlin.multiplatform"
    id "maven-publish"
    id "com.github.johnrengelman.shadow"
    id "com.jfrog.bintray"
}

kotlin {
    jvm()

    sourceSets {
        commonMain {
            dependencies {
                compileOnly kotlin('stdlib-common')
                compileOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

                compileOnly project(':jvm-package:jvm-publish-common')
                api project(':mapper-core')
                api project(':vis-svg-mapper')
                api project(':vis-svg-mapper-jfx')
                api project(':vis-demo-common')
                api project(':vis-demo-common-jfx')
            }
        }
        jvmMain {
            dependencies {
                compileOnly kotlin('stdlib-jdk8')
                compileOnly "org.slf4j:slf4j-simple:$slf4j_version"
            }
        }
    }
}


def artifactBaseName = 'lets-plot-jfx'
def artifactGroupId = project.group
def artifactVersion = project.version

def task_group = 'lets plot'

/*
 * Copyright (c) 2019. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

// https://discuss.kotlinlang.org/t/how-to-publish-fat-jar-from-kotlin-multi-project-mpp/14520
// https://youtrack.jetbrains.com/issue/KT-25709
// https://discuss.kotlinlang.org/t/kotlin-1-3-m2-new-multiplatform-projects-model/9264/107
task jarLetsPlotJvmJfxClasses(type: ShadowJar) {
    group task_group
    archiveBaseName = artifactBaseName
    archiveVersion = artifactVersion
    configurations = [project.configurations.jvmRuntimeClasspath]

    exclude '**/*.kotlin_metadata'
    exclude '**/*.kotlin_module'
    exclude '**/*.kotlin_builtins'

    // exclude classes that duplicates `lets-plot-common.jar`
    exclude 'com/google/**/*'
    exclude 'kotlin/**/*'
    exclude 'kotlinx/**/*'
    exclude 'mu/**/*'
    exclude 'org/intellij/lang/annotations/**/*'
    exclude 'org/jetbrains/annotations/**/*'
    exclude 'org/slf4j/**/*'
    exclude 'junit/**/*'
    exclude 'org/junit/**/*'
    exclude 'org/hamcrest/**/*'
}

task jarLetsPlotJvmJfxSources(type: ShadowJar) {
    group task_group
    archiveBaseName = artifactBaseName
    archiveVersion = artifactVersion
    classifier 'sources'
    configurations = [rootProject.configurations.letsPlotJvmJfxSources]
}


publishing {
    publications {
        letsPlotJvmJfx(MavenPublication) {
            groupId artifactGroupId
            artifactId artifactBaseName
            version artifactVersion

            artifact jarLetsPlotJvmJfxClasses
            artifact jarLetsPlotJvmJfxSources

            pom {
                name = "Lets-Plot for JavaFX"
                description = "Lets-Plot JVM package with JavaFX rendering"
                url = "https://github.com/JetBrains/lets-plot"
                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id = "jetbrains"
                        name = "JetBrains"
                        email = "lets-plot@jetbrains.com"
                    }
                }
                scm {
                    url = "https://github.com/JetBrains/lets-plot"
                }
                // Dependencies
                withXml {
                    def deps = asNode().appendNode('dependencies')
                    // Lets-Plot common
                    deps.appendNode('dependency')
                            .appendNode('groupId', artifactGroupId)
                            .appendNode('artifactId', 'lets-plot-common')
                            .appendNode('version', artifactVersion)
                    // Kotlin HTML
                    deps.appendNode('dependency')
                            .appendNode('groupId', 'org.jetbrains.kotlinx')
                            .appendNode('artifactId', 'kotlinx-html-jvm')
                            .appendNode('version', kotlinx_html_version)
                    // Hamcrest tests
                    deps.appendNode('dependency')
                            .appendNode('groupId', 'org.hamcrest')
                            .appendNode('artifactId', 'hamcrest-core')
                            .appendNode('version', hamcrest_version)
                }
            }
        }
    }
    repositories {
        maven {
//            url = uri("$rootDir/.maven-publish-dev-repo")
            url = uri("https://jetbrains.bintray.com/lets-plot-maven")
        }
    }
}

// Provide Bintray-specific Information
def bintray_user = project.buildSettings?.bintray?.user
def bintray_key = project.buildSettings?.bintray?.key

if (bintray_user != null && bintray_key != null) {
    bintray {
        publications = ['letsPlotJvmJfx']
        override = true

        user = bintray_user
        key = bintray_key
        publish = project.bintraySettings.publish

        pkg {
            repo = project.bintraySettings.mvn_repo
            name = project.bintraySettings.mvn_pkg_name
            userOrg = project.bintraySettings.userOrg
            licenses = project.bintraySettings.licenses
            vcsUrl = project.bintraySettings.vcsUrl
            version {
                name = artifactVersion
            }
        }
    }
}
