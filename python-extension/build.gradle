/*
 * Copyright (c) 2019. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

plugins {
    id "org.jetbrains.kotlin.multiplatform"
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

kotlin {

    if (project.buildSettings.enable_python_package) {

        def target
        if (DefaultNativePlatform.getCurrentOperatingSystem().macOsX & project.buildSettings.architecture == "x86_64") {
            target = macosX64("native")
        } else if (DefaultNativePlatform.getCurrentOperatingSystem().macOsX & project.buildSettings.architecture == "arm64") {
            target = macosArm64("native")
        } else if (DefaultNativePlatform.getCurrentOperatingSystem().linux & project.buildSettings.architecture == "x86_64") {
            target = linuxX64("native")
        } else if (DefaultNativePlatform.getCurrentOperatingSystem().linux & project.buildSettings.architecture == "arm64") {
            target = linuxArm64("native")
        } else if (DefaultNativePlatform.getCurrentOperatingSystem().windows) {
            target = mingwX64("native")
        } else {
            throw new Exception("Unsupported platform! Check project settings.")
        }

        target.binaries {
            staticLib {
                baseName = "lets-plot-${project.name}"
            }
        }
        target.compilations.main.cinterops {
            python {
                compilerOpts "-I${project.buildSettings.python.include_path}"
            }
        }
    } else {
        jvm() // at least one target is required by MPP - dummy jvm target will work just fine
    }

    sourceSets {
        all {
            languageSettings.optIn("kotlinx.cinterop.ExperimentalForeignApi")
        }

        nativeMain {
            dependencies {
                implementation kotlin('stdlib-common')

                implementation project(':commons')
                implementation project(':datamodel')
                implementation project(':plot-base')
                implementation project(':plot-builder')
                implementation project(':plot-stem')
                implementation project(':platf-native')
            }
        }
    }
}
