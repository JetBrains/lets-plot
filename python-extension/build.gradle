plugins {
    id "org.jetbrains.kotlin.multiplatform"
    id "org.jetbrains.gradle.plugin.idea-ext"
}


/*
 * Copyright (c) 2019. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

def currentOs = DefaultNativePlatform.getCurrentOperatingSystem()

kotlin {
    if (project.buildSettings.build_python_extension) {
        def target
        if (currentOs.isMacOsX()) {
            target = macosX64("native")
        } else if (currentOs.isLinux()) {
            target = linuxX64("native")
        } else if (currentOs.isWindows()) {
            target = mingwX64("native")
        } else {
            throw "Unsupported platform " + currentOs.displayName
        }

        target.binaries {
            staticLib {
                baseName = "lets-plot-${project.name}"
            }
        }

        target.compilations.main.cinterops {
            python {
                compilerOpts "-I${project.buildSettings.python.include_path}"
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')

                // (!) only `portable` sources
                implementation project(':base-portable')
                implementation project(':plot-config-portable')
            }
        }
    }
}