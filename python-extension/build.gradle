plugins {
    id "org.jetbrains.kotlin.multiplatform"
    id "org.jetbrains.gradle.plugin.idea-ext"
}


/*
 * Copyright (c) 2019. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

def currentOs = DefaultNativePlatform.getCurrentOperatingSystem()

kotlin {
    if (project.buildSettings.build_python_extension) {
        def target
        if (currentOs.isMacOsX()) {
            def currentArch = DefaultNativePlatform.getCurrentArchitecture()
            if (currentArch.displayName.contains("arm-v8")) {
                target = macosArm64("native")
            } else {
                target = macosX64("native")
            }
        } else if (currentOs.isLinux("native")) {
            target = linuxX64()
        } else if (currentOs.isWindows("native")) {
            target = mingwX64()
        } else {
            throw "Unsupported platform " + currentOs.displayName
        }

        target.binaries {
            staticLib {
                baseName = "lets-plot-${project.name}"
            }
        }

        target.compilations.main.cinterops {
            python {
                compilerOpts "-I${project.buildSettings.python.include_path}"
            }
        }
    } else {
        jvm() // at least one target is required by MPP - dummy jvm target will work just fine
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')

                // (!) only `portable` sources
                implementation project(':base-portable')
                implementation project(':plot-config-portable')
            }
        }
    }
}