plugins {
    id 'kotlin-multiplatform'
    id 'org.jetbrains.gradle.plugin.idea-ext'
}

kotlin {
    jvm("jvmJfx")
    jvm("jvmRawJfx")
    jvm("jvmBrowser")
    js() {
        compileKotlinJs {
            kotlinOptions.sourceMap = true
            kotlinOptions.sourceMapEmbedSources = "always"
        }
    }


    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
                implementation project(':base-portable')
                implementation project(':base')
                implementation project(':gis')
                implementation project(':livemap')
                implementation project(':vis-svg')
                implementation project(':vis-canvas')
                implementation project(':vis-demo-common')
                implementation project(':plot-builder-portable')
                implementation project(':plot-builder')
                implementation project(':plot-base-portable')
                implementation project(':plot-base')
            }
        }
        allJvm {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                compileOnly "io.github.microutils:kotlin-logging:$kotlinLogging_version"
                implementation "org.jetbrains.kotlinx:kotlinx-html-jvm:${kotlinx_html_version}"
                implementation "io.ktor:ktor-client-cio:$ktor_version"
            }
        }
        jvmMain {
            dependsOn allJvm
        }
        jvmJfxMain {
            dependsOn allJvm
        }
        jvmRawJfxMain {
            dependsOn allJvm
        }
        jvmBrowserMain {
            dependsOn allJvm
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }
    }
}

idea {
    module {
        settings {
            packagePrefix["src/jvmJfxMain/kotlin"] = ""
            packagePrefix["src/jvmBrowserMain/kotlin"] = "jetbrains"
        }
    }
}

/*
 * Copyright (c) 2019. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

// Workaround for Idea/Gradle bug: https://youtrack.jetbrains.com/issue/KT-24463
// MPP: Run does not add resource directory to classpath [Cannot get resource when using common module]
//
// JavaFX Scene mapping requires stylesheet resource URI
task copyProcessedResources(type: Copy) {
    from "${project.buildDir}/processedResources"
    into "${project.buildDir}/classes/kotlin"
}

build.dependsOn += copyProcessedResources


// ---------------------------------
// Trick to copy kotlin.js etc. to `output`
configurations {
    libraryDependenciesJs
    projectDependenciesJs {
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    libraryDependenciesJs "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    libraryDependenciesJs "io.github.microutils:kotlin-logging-js:$kotlinLogging_version"
    // for Assertions.kt ToDo: get rid of kotlin-test here
    libraryDependenciesJs "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
    libraryDependenciesJs "io.ktor:ktor-client-websockets:$ktor_version"
    libraryDependenciesJs "io.ktor:ktor-client-cio:$ktor_version"
    libraryDependenciesJs "io.ktor:ktor-client-js:$ktor_version"

    projectDependenciesJs project(path: ':base-portable', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':base', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':gis', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':livemap', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':mapper-core', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':vis-svg', configuration: 'outputJsJar')

    projectDependenciesJs project(path: ':vis-demo-common', configuration: 'outputJsJar')

    projectDependenciesJs project(path: ':vis-canvas', configuration: 'outputJsJar')
    // required by plot-builder (get rid?)

    projectDependenciesJs project(path: ':plot-common-portable', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':plot-common', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':plot-base', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':plot-base-portable', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':plot-builder-portable', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':plot-builder', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':plot-config-portable', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':plot-config', configuration: 'outputJsJar')
}

// ---------------------------------
def OUTPUT_DIR_JS = "${project.buildDir}/demoWeb"
def OUTPUT_DIR_JS_LIB = "${OUTPUT_DIR_JS}/lib"

task copyLibraryDependenciesJs(type: Copy) {
    configurations.libraryDependenciesJs.each {
        println "library dependency: ${it.absolutePath}"
        from zipTree(it.absolutePath).matching { include '*.js' }
    }
    into OUTPUT_DIR_JS_LIB
}

task copyProjectDependenciesJs(type: Copy) {
    configurations.projectDependenciesJs.each {
        def jarJs = it.absolutePath.replace("-metadata-", "-js-")
        println "project dependency: $jarJs"
        from zipTree(jarJs).matching { include '*.js' }
    }
    into OUTPUT_DIR_JS_LIB
}

task copyMainJs(type: Copy) {
    from "${project.buildDir}/classes/kotlin/js/main"
    include "*.js"
    into OUTPUT_DIR_JS
}

build.dependsOn += copyLibraryDependenciesJs
build.dependsOn += copyProjectDependenciesJs
build.dependsOn += copyMainJs