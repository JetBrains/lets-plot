/*
 * Copyright (c) 2019. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id "org.jetbrains.kotlin.multiplatform"
    id "org.jetbrains.gradle.plugin.idea-ext"
}

kotlin {
    jvm()
    js {
        browser()
    }

    if (project.buildSettings.build_python_extension) {
        def currentOs = DefaultNativePlatform.getCurrentOperatingSystem()

        if (currentOs.isMacOsX()) {
            macosX64("native")
        } else if (currentOs.isLinux()) {
            linuxX64("native")
        } else if (currentOs.isWindows()) {
            mingwX64("native")
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation kotlin('test-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                compileOnly kotlin('test-js')
            }
        }
    }
}

idea {
    module {
        settings {
            packagePrefix["src/jvmMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmTest/kotlin"] = "jetbrains.datalore"
        }
    }
}
