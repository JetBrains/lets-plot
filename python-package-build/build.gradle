import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id "base"
}

/*
 * Copyright (c) 2019. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

// ----------------------------------------
// Building and publishing Python package
// ----------------------------------------

def currentOs = DefaultNativePlatform.getCurrentOperatingSystem()
def currentOsArch = DefaultNativePlatform.getCurrentArchitecture()

//def task_group = 'lets plot'
def tools_dir = "${rootDir}/tools"
def python_package_dir = "${rootDir}/python-package"
def python_package_build_dir = "${python_package_dir}/build"
def python_package_dist_dir = "${python_package_dir}/dist"

clean {
    delete python_package_build_dir
    delete python_package_dist_dir
    delete "${python_package_dir}/lets_plot/package_data"
    delete "${python_package_dir}/lets_plot.egg-info"
}

if (project.buildSettings.enable_python_package) {

    def python_bin_path = project.buildSettings.python.bin_path

    task buildPythonPackage(type: Exec) {
        group = project.letsPlotTaskGroup
        description = 'Builds lets-plot wheel distribution (python)'

        workingDir python_package_dir
        if (currentOs.isWindows()) {
            commandLine "${python_bin_path}/python",
                    'setup.py',
                    'update_js',
                    'bdist_wheel',
                    "--dist-dir=${python_package_dist_dir}",
                    'build',
                    '-c',
                    'mingw32'
        } else {
            commandLine "${python_bin_path}/python3",
                    'setup.py',
                    'update_js',
                    'bdist_wheel',
                    "--dist-dir=${python_package_dist_dir}"
        }
    }

    task buildManylinuxWheels(type: Exec) {
        group = project.letsPlotTaskGroup
        description = 'Builds lets-plot wheel distribution with Manylinux platform for publication(python)'

        workingDir tools_dir
        commandLine "./run_manylinux_docker.sh", "${rootDir}", "${currentOsArch}"
    }


    def pypi_prod = project.buildSettings.pypi.prod
    if (pypi_prod.username != null && pypi_prod.password != null) {
        task publishProdPythonPackage(type: Exec) {
            group = project.letsPlotTaskGroup
            description = 'Publishes lets-plot python package to pypi.org'

            workingDir python_package_dist_dir
            commandLine "${python_bin_path}/twine",
                    "upload",
                    "-u",
                    pypi_prod.username,
                    "-p",
                    pypi_prod.password,
                    "./*"
        }
    }

    def pipy_test = project.buildSettings.pypi.test
    if (pipy_test.username != null && pipy_test.password != null) {
        task publishTestPythonPackage(type: Exec) {
            group = project.letsPlotTaskGroup
            description = 'Publishes lets-plot python package to test.pypi.org'

            workingDir python_package_dist_dir
            commandLine "${python_bin_path}/twine",
                    "upload",
                    "--repository-url",
                    "https://test.pypi.org/legacy/",
                    "-u",
                    pipy_test.username,
                    "-p",
                    pipy_test.password,
                    "./*"
        }
    }

    buildPythonPackage.dependsOn(':js-package:build')
    buildPythonPackage.dependsOn(':python-extension:build')

    build.dependsOn(buildPythonPackage)

    //publishPythonPackage.dependsOn(buildPythonPackage)
}