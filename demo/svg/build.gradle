/*
 * Copyright (c) 2019. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

plugins {
    id "org.jetbrains.kotlin.multiplatform"
}

kotlin {
    jvm("jvmBatik")
    jvm("jvmJfx")
    jvm("jvmBrowser")   // generates index.html and opens it in browser
    js(LEGACY) {
        browser {
            dceTask {
                keep.addAll(
                        "lets-plot-demo-svg.demo.svgMapping.svgElementsDemoA",
                        "lets-plot-demo-svg.demo.svgMapping.svgElementsDemoB",
                )
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')

                implementation project(':commons')
                implementation project(':datamodel')
                implementation project(':demo-common-util')
            }
        }

        allJvm {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                compileOnly "io.github.microutils:kotlin-logging-jvm:$kotlinLogging_version"
            }
        }
        jvmBatikMain {
            dependsOn allJvm
            dependencies {
                implementation project(":platf-batik")
                implementation project(':demo-common-batik')

                implementation("org.apache.xmlgraphics:batik-codec:$batik_version")
            }
        }
        jvmJfxMain {
            dependsOn allJvm
            dependencies {
                implementation project(':canvas') // needed for `svg transform` parsing
                implementation project(":platf-jfx-swing")
                implementation project(':demo-common-jfx')
            }
        }
        jvmBrowserMain {
            dependsOn allJvm
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-html-jvm:${kotlinx_html_version}"
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')

                implementation project(":platf-w3c")
            }
        }
    }
}
